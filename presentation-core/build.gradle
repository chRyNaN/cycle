import com.chrynan.presentation.buildSrc.LibraryConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "com.android.library"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    targets {
        android()
        jvm()
        js(BOTH) {
            browser()
            nodejs()
        }
        ios()
    }
    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation("kotlin.RequiresOptIn")
            }
        }
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"

                // Kotlin Coroutines
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"

                // Coroutine Dispatchers
                api "com.chrynan.dispatchers:dispatchers-core:0.2.1"

                // Mapper
                api "com.chrynan.mapper:mapper-core:1.5.1"
            }
        }
    }
}

android {
    compileSdkVersion LibraryConstants.Android.compileSdkVersion

    defaultConfig {
        minSdkVersion LibraryConstants.Android.minSdkVersion
        targetSdkVersion LibraryConstants.Android.targetSdkVersion
        versionCode LibraryConstants.versionCode
        versionName LibraryConstants.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
            java.srcDirs = ["src/androidMain/kotlin"]
            res.srcDirs = ["src/androidMain/res"]
        }
        test {
            java.srcDirs = ["src/androidTest/kotlin"]
            res.srcDirs = ["src/androidTest/res"]
        }
    }
}

// Android Specific Dependencies
dependencies {
    implementation "androidx.activity:activity-ktx:1.3.0-alpha08"
    implementation "androidx.fragment:fragment-ktx:1.4.0-alpha01"
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "androidx.appcompat:appcompat:1.3.0"
}

configurations {
    compileClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url "https://repo.repsy.io/mvn/chrynan/public"

                credentials {
                    username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                    password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release
                groupId LibraryConstants.group
                artifactId "presentation-android"
                version LibraryConstants.versionName
            }
        }
    }
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
